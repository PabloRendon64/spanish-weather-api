plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.4'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

group = 'com.ferchau.spain.weather'
version = '0.0.1-SNAPSHOT'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// https://mvnrepository.com/artifact/org.springframework.cloud/spring-cloud-starter-openfeign
	implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.3'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// https://mvnrepository.com/artifact/org.mapstruct/mapstruct
	implementation 'org.mapstruct:mapstruct:1.5.2.Final'
	// https://mvnrepository.com/artifact/org.mapstruct/mapstruct-processor
	annotationProcessor  'org.mapstruct:mapstruct-processor:1.5.2.Final'

	// https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-jsr310
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.18.0'

	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

	// https://mvnrepository.com/artifact/org.assertj/assertj-core
	testImplementation 'org.assertj:assertj-core:3.23.1'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

}

tasks.named('test') {
	useJUnitPlatform()
}

//code coverage
test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
	dependsOn test // tests are required to run before generating the report

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, include: [
					'com/ferchau/spain/weather/domain/usecase/**',
					'com/ferchau/spain/weather/infrastructure/rest/**',
					'com/ferchau/spain/weather/entrypoint/controller/**',
			], excludes: [
					'**/mapper/**',
			])
		}))
	}
}

jacoco {
	toolVersion = "0.8.12"
	reportsDirectory = layout.buildDirectory.dir('jacoco')
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 1
			}
		}

		rule {
			enabled = false
			element = 'CLASS'
			includes = [
					'com.ferchau.spain.weather.domain.usecase.*',
					'com.ferchau.spain.weather.infrastructure.rest.*'
			]
			excludes = [
					'com.ferchau.spain.weather.infrastructure.rest.aemet.mapper.*',
			]
		}
	}
}
